dnl Copyright (C) 2000-2002 Free Software Foundation, Inc.
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

dnl From Bruno Haible.

AC_DEFUN([AM_EXPAT_LINKFLAGS_BODY],
[
  dnl Prerequisites of AC_LIB_LINKFLAGS_BODY.
  AC_REQUIRE([AC_LIB_PREPARE_PREFIX])
  AC_REQUIRE([AC_LIB_RPATH])

  dnl Search for libexpat and define LIBEXPAT, LTLIBEXPAT and INCEXPAT
  dnl accordingly.
  AC_LIB_LINKFLAGS_BODY([expat])
])

AC_DEFUN([AM_EXPAT_LINK],
[
  dnl Search for libexpat and define LIBEXPAT, LTLIBEXPAT and INCEXPAT
  dnl accordingly.
  AC_REQUIRE([AM_EXPAT_LINKFLAGS_BODY])

  dnl Add $INCEXPAT to CPPFLAGS before performing the following checks,
  dnl because if the user has installed libexpat and not disabled its use
  dnl via --without-libexpat-prefix, he wants to use it. The first
  dnl AC_TRY_LINK will then fail, the second AC_TRY_LINK will succeed.
  am_save_CPPFLAGS="$CPPFLAGS"
  AC_LIB_APPENDTOVAR([CPPFLAGS], [$INCEXPAT])

  AC_CACHE_CHECK(for expat, am_cv_lib_expat, [
    am_cv_lib_expat=no
    am_save_LIBS="$LIBS"
    LIBS="$LIBS $LIBEXPAT"
    AC_TRY_LINK([#include <expat.h>],
      [XML_ParserCreate(NULL);],
      am_cv_lib_expat=yes)
    LIBS="$am_save_LIBS"
  ])

  if test "$am_cv_lib_expat" = yes; then
    AC_DEFINE(HAVE_LIBEXPAT, 1, [Define if you have the libexpat.])
    AC_MSG_CHECKING([how to link with libexpat])
    AC_MSG_RESULT([$LIBEXPAT])
  else
    dnl If $LIBEXPAT didn't lead to a usable library, we don't need $INCEXPAT
    dnl either.
    CPPFLAGS="$am_save_CPPFLAGS"
    LIBEXPAT=
    LTLIBEXPAT=
  fi
  AC_SUBST(LIBEXPAT)
  AC_SUBST(LTLIBEXPAT)
])
